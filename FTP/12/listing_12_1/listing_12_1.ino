//// Подключаем библиотеку управления сервомоторами.
#include <Servo.h>
// Подключаем библиотеку, управляющую моторами.
#include "motor.h"
// Подключаем библиотеку, управляющую дальномером.
#include "sonar.h"
// Создаем сервомотор поворота головы.
Servo neck;
// Константы - постоянные значения для уточнения углов.
const int left_ang=168; //Без поправки 160
const int front_ang=98; // Без поправки 90
const int right_ang=28; // Без поправки 20
// Временные константы служат для точного задания времени на поворот, разворот, движение вперед.
// в миллисекундах.
const int time_90=390;
const int time_180=750;
const int time_10cm=220;
const int time_7cm=120;
void setup()
{
   // Инициализируем дальномер Trig = 13, Echo = 12.
  Sonar_init(13, 12);
  // Инициализируем сервомотор, управление 9 портом.
  neck.attach(14);
  // Переменные – номера контактов (пинов) Arduino.
  // Для левых и правых моторов машинки.
  setup_motor_system(2, 3, 4, 5);
  _stop(); //Двигатели остановлены.
}
// Основная программа.
void loop()
{
  // Создаем переменные для хранения двух 
  //дистанций до препятствий спереди, справа.
  int Dist_left, Dist_front, Dist_right;
  _stop(); 
  // Ждем, так как поворот занимает небольшое время.
  delay(250);
  // Записываем расстояние до препятствия впереди.
  Dist_front = Sonar(40);
  // Поворачиваем голову направо.
  neck.write(right_ang);
  // Ждем, так как поворот занимает небольшое время.
  delay(250);
  // Записываем расстояние до препятствия впереди.
  Dist_right = Sonar(40);
  neck.write(front_ang);
  // Если условия позволяют двигаться прямо
  if((Dist_right>=8)&&(Dist_right<=15)&&(Dist_front>=15))
  {
    forward(); // едем вперед.
    delay(time_10cm);
  }
  else
  {// если появился поворот направо
     if(Dist_right>15)
     {
       // если абсолютно уперлись в стену!
       // узкий правый поворот.
       if(Dist_front<5)
       {
        right(); // поворачиваем направо.
        delay(time_90);
       }
       else // нормальный правый поворот.
       { // поворот направо в движении.
       right(); 
       delay(time_90/3);        
       forward(); // едем вперед 7 см.
       delay(time_90/3);
       }
     }
     else
     // если уперлись в стену.
     if(Dist_front<15)
     {
       left(); // поворачиваем налево.
       delay(time_90);
     }
     else
    { // подворот налево в движении.
     left(); 
     delay(time_90/3);       
     forward(); // едем вперед 5 см.
     delay(time_90/3);
    }
  }
}

